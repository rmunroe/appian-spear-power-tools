//buildscript {
//    dependencies {
//        classpath 'org.hidetake:gradle-ssh-plugin:2.10.1'
//    }
//}

plugins {
    id 'java'
    id 'groovy'
    id 'org.owasp.dependencycheck' version '7.3.0'
    id 'org.hidetake.ssh' version '2.11.2'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib-compile'
    }
}

dependencies {
    compileOnly fileTree(dir: 'lib-compile', include: '*.jar')
    implementation fileTree(dir: 'lib-include', include: '*.jar')

    implementation 'com.github.f4b6a3:uuid-creator:5.1.2'
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.4.Final'
    implementation 'pl.allegro.finance:tradukisto:1.12.0'

    // These are found in Appian's classpath (<APPIAN_HOME>/deployment/web.war/WEB-INF/lib)
    compileOnly 'org.apache.logging.log4j:log4j-1.2-api:2.17.1'
    compileOnly 'commons-codec:commons-codec:1.13'
    compileOnly 'commons-io:commons-io:2.9.0'
    compileOnly 'com.google.guava:guava:31.1-jre'
}


configurations {
    implementation.transitive = false
}


jar {
    dependsOn check
    duplicatesStrategy = DuplicatesStrategy.FAIL

    configurations.implementation.setCanBeResolved(true)

    into('META-INF/lib') {
        from(configurations.implementation)
    }

    into('src') {
        from(sourceSets.main.allJava)
    }

    manifest {
        attributes("Spring-Context": "*;publish-context:=false")
    }
}


archivesBaseName = "SPEaR Power Tools"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


version = new groovy.xml.XmlSlurper().parse(file('src/main/resources/appian-plugin.xml')).'plugin-info'.version


remotes {
    appian {
        host = '10.0.0.26'
        user = 'appian'
        password = 'appian'
    }
}

task deployToVm {
    group = 'install'
    dependsOn jar

    doLast {
        ssh.run {
            session(remotes.appian) {
                put from: "$projectDir/build/libs/${rootProject.name}-${version}.jar", into: '/opt/appian/23.2/_admin/plugins/'
            }
        }
    }
}